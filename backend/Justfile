root := justfile_directory()
compose_project := env_var_or_default("COMPOSE_PROJECT_NAME", "backend")
compose := "docker compose -f docker-compose.yml -p " + compose_project

sqlite_dev_url := "sqlite://" + root + "/data/modunote_dev.sqlite"
sqlite_test_url := "sqlite://" + root + "/data/modunote_test.sqlite"

postgres_dev_url := "postgres://modunote:modunote@localhost:5432/modunote_dev?sslmode=disable"
postgres_test_url := "postgres://modunote:modunote@localhost:5433/modunote_test?sslmode=disable"

install:
    rustup show active-toolchain
    cargo fetch

postgres-dev-up:
  {{compose}} up -d postgres_dev

postgres-dev-reset:
  {{compose}} rm -sf postgres_test
  docker volume rm {{compose_project}}_postgres_dev_data

postgres-test-up:
  {{compose}} up -d postgres_test

postgres-test-down:
  {{compose}} rm -sf postgres_test

postgres-test-reset:
  {{compose}} rm -sf postgres_test
  docker volume rm {{compose_project}}_postgres_test_data

migrate-sqlite-dev:
  mkdir -p {{root}}/data
  touch {{root}}/data/modunote_dev.sqlite
  sqlx migrate run --source crates/storage_sqlite/migrations --database-url {{sqlite_dev_url}}

migrate-postgres-dev:
  sqlx migrate run --source crates/storage_postgres/migrations --database-url {{postgres_dev_url}}

test-sqlite:
  mkdir -p {{root}}/data
  rm -f {{root}}/data/modunote_test.sqlite
  touch {{root}}/data/modunote_test.sqlite
  sqlx migrate run --source crates/storage_sqlite/migrations --database-url {{sqlite_test_url}}
  DATABASE_URL={{sqlite_test_url}} cargo test -p storage-sqlite
  rm -f {{root}}/data/modunote_test.sqlite

test-postgres:
  #!/usr/bin/env bash
  # Stop the script immediately if 'up' fails
  set -e
  
  cleanup() {
    echo "Cleaning up..."
    just postgres-test-reset
  }
  
  # Always run cleanup on exit
  trap cleanup EXIT

  # Start the environment
  just postgres-test-up
  for i in {1..30}; do
    if {{compose}} exec -T postgres_test pg_isready -U modunote -d modunote_test >/dev/null 2>&1; then
      break
    fi
    sleep 1
  done
  sqlx migrate run --source crates/storage_postgres/migrations --database-url {{postgres_test_url}}

  # Run tests
  DATABASE_URL={{postgres_test_url}} cargo test -p storage-postgres
